## .complete - customizations for tcsh (6.02 or above) completion feature

if ($?prompt) echo "Running .complete..."

## complete with user names
# complete ps			'n/-*u/u/' 'c/-*u/u/'
# complete finger			'n/*/u/'

## protect some special files
complete rm		'n/*/f:^{.*,*.{c,cc,C},*.{h,hh,H},*.scm,*.el{,c}}/'

## complete with environ vars
complete {{,set,unset,print}env}	'p/1/e/'
complete unalias		'p/1/a/'

## complete certain env vars with paths
complete env                    'c/CC=/f/' 'c/CXX=/f/' 'c/LD_LIBRARY_PATH=/f/'

## complete with jobs
complete {-%*,fg,bg}		'c/%/j/'
complete kill			'c/-/S/' 'c/%/j/'

## complete with directories
complete {cd,pd,chdir,pushd}	'p/1/d/'

complete latex			'n/*/f:*.{tex,ltx}/'
complete {xdvi,dvips,dvitty}	'n/*/f:*.dvi/'
complete {ghostview,gv,gs}	'n/*/f:*.{ps,eps,pdf,PS,EPS,PDF}/'
complete xpdf			'n/*/f:*.{pdf,PDF}/'

complete javac			'n/*/f:*.java/'

## Complete -I and -L flags to gcc/cc with directories.
complete *cc			'c/-I/d/' 'c/-L/d/'
complete *cc4			'c/-I/d/' 'c/-L/d/'
complete {g++,c++}		'c/-I/d/' 'c/-L/d/'
complete {g++4,c++4}		'c/-I/d/' 'c/-L/d/'

## Complete the of= and if= options to dd with filenames.
complete dd			'c/if=/f/' 'c/of=/f/'

## Complete file:// urls with local filenames
## complete the tag and branch portions of sport svn urls
## the sed part is to get around autocompletion adding more slashes
## Note we have to pseudo-quote the ^ since tcsh thinks we are trying to negate the glob
## and it crashes.  So we put the ^ within a singular glob.
complete svn \
'p/1/(add blame annotate cat checkout cleanup commit copy delete remove diff export help import info list lock log merge mergeinfo mkdir move rename propdel propedit propget proplist propset resolved revert status switch unlock update)/' \
'c|file://|f|' \
'c,{^}/branches/,`svn info | sed -n s/Repository.Root:.//p | xargs -i svn ls "{}/branches" | sed sX/XX`,/,' \
'c,{^}/tags/,`svn info | sed -n s/Repository.Root:.//p | xargs -i svn ls "{}/tags" | sed sX/XX`,/,' \
'c,{^}/,`svn info | sed -n s/Repository.Root:.//p | xargs svn ls | sed sX/XX`,/,' \
'c,svn://svn/va/branches/,`svn ls svn://svn/va/branches | sed sX/XX`,/,' \
'c,svn://svn/va/tags/,`svn ls svn://svn/va/tags | sed sX/XX`,/,' \
'c,svn://svn/va/,`svn ls svn://svn/va | sed sX/XX`,/,' \
'c,svn://svn/rbtcore/branches/,`svn ls svn://svn/rbtcore/branches | sed sX/XX`,/,' \
'c,svn://svn/rbtcore/tags/,`svn ls svn://svn/rbtcore/tags | sed sX/XX`,/,' \
'c,svn://svn/rbtcore/,`svn ls svn://svn/rbtcore | sed sX/XX`,/,' \
'c,svn://svn/sport/branches/,`svn ls svn://svn/sport/branches | sed sX/XX`,/,' \
'c,svn://svn/sport/tags/,`svn ls svn://svn/sport/tags | sed sX/XX`,/,' \
'c,svn://svn/sport/,`svn ls svn://svn/sport | sed sX/XX`,/,' \
'c,svn://svn/va-product/branches/,`svn ls svn://svn/va-product/branches | sed sX/XX`,/,' \
'c,svn://svn/va-product/tags/,`svn ls svn://svn/va-product/tags | sed sX/XX`,/,' \
'c,svn://svn/va-product/,`svn ls svn://svn/va-product | sed sX/XX`,/,' \
'c,svn://svn/sh-product/branches/,`svn ls svn://svn/sh-product/branches | sed sX/XX`,/,' \
'c,svn://svn/sh-product/tags/,`svn ls svn://svn/sh-product/tags | sed sX/XX`,/,' \
'c,svn://svn/sh-product/,`svn ls svn://svn/sh-product | sed sX/XX`,/,'

complete git \
'p/1/(add bisect branch checkout clone commit diff fetch grep init log merge mv pull push rebase reset rm show status tag stash stat   svn svn-in svn-out co br ci)/' \
'n/svn/(init fetch clone rebase dcommit branch tag log blame find-rev set-tree create-ignore show-ignore mkdirs commit-diff info proplist propget show-externals gc reset)/' \
'n/co/`git br -a`/' \
'n/checkout/`git br -a`/' \
'n/log/`git br -a`/' \
'n/rebase/`git br -a`/'

## Complete -M flag to man with directories.
complete man			'c/-M/d/'

## complete "service" command with stuff from /etc/rc.d/init.d
complete su1			'p/1/c/' 'n|service|F:/etc/rc.d/init.d|'
complete sudo                   'p/1/c/' 'n|service|F:/etc/rc.d/init.d|'

## complete, e.g., --with-tmslib=DIR
complete *configure		'c/--with-*=/d/'
